plugins {
	id 'java-library'
	id 'eclipse'
	id 'idea'
	id 'maven-publish'
	id 'net.neoforged.moddev' version '2.0.+'
}

tasks.named('wrapper', Wrapper).configure {
    // Define wrapper values here so as to not have to always do so when updating gradlew.properties.
    // Switching this to Wrapper.DistributionType.ALL will download the full gradle sources that comes with
    // documentation attached on cursor hover of gradle classes and methods. However, this comes with increased
    // file size for Gradle. If you do switch this to ALL, run the Gradle wrapper task twice afterwards.
    // (Verify by checking gradle/wrapper/gradle-wrapper.properties to see if distributionUrl now points to `-all`)
    distributionType = Wrapper.DistributionType.BIN
}

version = "${mod_version}"
group = "net.commoble.${modid}" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
base {
	archivesName = "${modid}"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

sourceSets
{
	main
	examplemod
}

//minecraft.accessTransformers.file rootProject.file('src/main/resources/META-INF/accesstransformer.cfg')

neoForge {
	version = "${neo_version}"
	mods {
		"${modid}" {
			sourceSet(sourceSets.main)
		}
		"infiniverse_examplemod" {
			sourceSet(sourceSets.examplemod)
		}
	}
	runs {
		client {
			client()
		}
		server {
			server()
		}
		gameTestServer {
			type = "gameTestServer"
		}
		configureEach {
			
		}
	}
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

configurations {
	examplemodCompileClasspath.extendsFrom(compileClasspath)
	examplemodRuntimeClasspath.extendsFrom(runtimeClasspath)
}

repositories {
}

dependencies {
	examplemodImplementation sourceSets.main.output
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	archiveClassifier = "javadoc"
	from javadoc.destinationDir
}

task sourcesJar(type: Jar) {
	archiveClassifier = "sources"
	from sourceSets.main.allJava
}

artifacts {
	archives javadocJar
	archives sourcesJar
}

// run "gradlew build publish" to build jars and generate a maven repo for them
// run "gradlew build" to build the jars without mavening them
publishing {
	publications {
		register('mavenJava', MavenPublication) {
			from components.java
            artifact sourcesJar
            artifact javadocJar
		}
	}
	repositories {
		maven {
			name="localMaven"
			url = "./maven"
		}
	}
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
